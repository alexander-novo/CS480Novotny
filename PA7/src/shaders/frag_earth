#version 330 core

// Interpolated values from the vertex shaders
in vec2 uvCoord;
in vec3 positionW;
in vec3 eyeT;
in vec3 lightDirT;

// Output data
out vec4 frag_color;

// Values that stay constant for the whole mesh.
uniform mat4 modelMatrix;
uniform mat4 viewMatrix;

uniform vec3 MaterialDiffuseColor;
uniform vec3 MaterialAmbientColor;
uniform vec3 MaterialSpecularColor;

uniform vec3 lightW3;

uniform float lightPower;

uniform sampler2D gSampler;
uniform sampler2D gAltSampler;
uniform sampler2D gNormalSampler;
uniform sampler2D gSpecularSampler;

void main(void) {
    //What color
    vec3 lightColor = vec3(1.0, 1.0, 1.0);

    // Normal of the computed fragment, in Tangent space
    vec3 n = normalize( texture2D(gNormalSampler, uvCoord).rgb * 2.0 - 1.0 );
    // Direction of the light (from the fragment to the light)
    vec3 l = normalize( lightDirT );

    // Cosine of the angle between the normal and the light direction
    // clamped to a minimum of 0
    // Anything facing directly towards the light will shine the brightest
    // and anything facing away will be black
    float cosTheta = dot( n,l );

    //How far away we are - light depends on distance
    float distance = length(lightW3 - positionW);

    if(cosTheta > -0.05) {
        vec3 MaterialDiffuseTexture2d = texture2D(gSampler, uvCoord.st).xyz;

        //Start with a basis of the diffuse color. This will later become our texture
        vec3 materialAmbientModified = (MaterialAmbientColor) * MaterialDiffuseTexture2d;

        // Eye vector (towards the camera)
        vec3 E = normalize(eyeT);
        // Direction in which the triangle reflects the light
        vec3 R = reflect(-l,n);
        // Cosine of the angle between the Eye vector and the Reflect vector,
        // clamped to 0
        //  - Looking into the reflection -> 1
        //  - Looking elsewhere -> < 1
        float cosAlpha = clamp( dot( E,R ), 0,1 );

        frag_color.rgb =
            // Ambient : simulates indirect lighting
            materialAmbientModified +
            // Diffuse : "color" of the object
            MaterialDiffuseTexture2d * lightColor * lightPower * abs(cosTheta) / distance +
            // Specular : reflective highlight, like a mirror
            texture2D(gSpecularSampler, uvCoord).rgb * lightColor * lightPower * pow(cosAlpha,5) / distance;

        if(cosTheta <= 0.05) {
            cosTheta += 0.05;
            cosTheta *= 10;

            frag_color.rgb *= cosTheta;
            frag_color.rgb += texture2D(gAltSampler, uvCoord.st).xyz * (1 - cosTheta);
        }
    }  else {
        frag_color.rgb = texture2D(gAltSampler, uvCoord.st).xyz;
    }
    frag_color.a = 1;
}

